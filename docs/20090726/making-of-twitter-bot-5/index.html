
<html>
<head>
<title>Twitter ボットの作り方 Perl 編 (5) - OTCHY.NET</title>
<link rel=stylesheet href=//cdnjs.cloudflare.com/ajax/libs/mini.css/3.0.1/mini-default.min.css>
<style></style>
</head>
<body>
<nav>
<a href=/ >HOME</a>
</nav>
<header>
<h1>Twitter ボットの作り方 Perl 編 (5)</h1>
</header>
<main>
Twitter ボットの作り方解説第 5 弾。ボットに対してのつぶやきを検索して、検索結果を返信するボットを作ってみます。<br>
以下は、第 4 弾までの状態を前提としています。<br>
<br>
さて、当初の予定だと Google 検索を行った結果を返すボットを作ろうとしていたのですが、Google 検索 API は公開をやめてしまっているのですね。知りませんでした…。<br>
こういう時は Yahoo の出番という事で、代わりに Yahoo 検索の結果を返すボットを作ります。<br>
Yahoo の様々な公開 API 群は、Google の後追いで公開を始めただけあって優れたものが多いのですが、いかんせん Google の陰に隠れがちですね。<br>
<br>
<h4>CPAN ライブラリの追加</h4>
例によって、Yahoo 検索を便利に実行するためのライブラリが CPAN に登録されているので、インストールしておきます。<br>
<pre>$ su -
# perl -MCPAN -e shell
<u>cpan></u> install Yahoo::Search
</pre>
<br>
<h4>アプリケーション ID の取得</h4>
Yahoo の検索 API を使うためには、あらかじめアプリケーション ID というものを取得する必要があります。<br>
Yahoo のアカウントを持っていれば、簡単に取得出来るので、以下のサイトからあらかじめ取得しておきます。<br>
<a href=http://developer.yahoo.co.jp/ target=_blank>Yahoo! デベロッパーネットワーク</a><br>
<br>
大手のウェブサービスで API を公開している場合、「API キー」とか、「デベロッパー ID」とか、呼び方はそれぞれですが、大抵はのこアプリケーション ID のようなものが用意されています。<br>
これらの ID は、各 API が過剰に利用されていないかを監視したり、必要に応じて利用を止めたりする目的で用意されています。<br>
とはいえ、Yahoo 検索のケースで、1日あたり 5000 回とかなので、普通の個人が利用する分には制限についてさほど気にする必要はありません。<br>
<br>
<h4>設定ファイルの準備</h4>
今回作成するボットは、第 3 弾で作った特定ワードに返信するボットと、基本的な作りは一緒です。<br>
設定には前述のアプリケーション ID を追加し、最後のつぶやきの管理には、sinceId を使います。<br>
<br>
config.yml<br>
<pre>username: "your_twitter_user_name"
password: "your_twitter_password"
appId: "your_yahoo_application_id"
</pre>
<br>
status.yml<br>
<pre>sinceId: 0
</pre>
<br>
<h4>実際のスクリプト</h4>
<pre>#!/usr/bin/perl

# 使用するモジュールの読み込み
use strict;
use warnings;
use Encode;
use FindBin;
use YAML::Tiny;
use Net::Twitter;
use Yahoo::Search;

# yml ファイルの読み込みと、Twitter モジュールの初期化
my $config = (YAML::Tiny->read($FindBin::Bin . '/config.yml'))->[0];
my $status = (YAML::Tiny->read($FindBin::Bin . '/status.yml'))->[0];
my $twit = Net::Twitter->new(username => $config->{'username'}, password => $config->{'password'});

# 各種設定値の取得
my $username = $config->{'username'};
my $appId = $config->{'appId'};
my $sinceId = $status->{'sinceId'};

# Twitter 検索。to: オプションでボット宛のつぶやきすべてを取得
my $response = $twit->search({q => 'to:' . $username, since_id => $sinceId});

# Twitter 検索結果を 1 件ずつ処理
for my $result ( reverse( @{$response->{'results'}} ) ) {
    my $query = Encode::encode('utf8', $result->{'text'});
    my $fromUser = $result->{'from_user'};
    my $statusId = $result->{'id'};

    # ボット宛の @ 部分を取り除く
    $query =~ s/\s*\@${username}\s*//;

    # Yahoo 検索 (最大5件、日本語圏で検索)
    my @results = Yahoo::Search->Results(
        Doc => $query,
        AppId => $appId,
        Count => 5,
        Language => 'ja'
    );

    # Yahoo 検索結果を 1 件ずつ処理
    for my $page (@results) {
        # ボットに対するつぶやきへの返信
        my $reply = '@' . $fromUser . ' ' . $page->Title . ' ' . $page->Url;
        $twit->update({status => $reply, in_reply_to_status_id => $statusId});
    }
    $sinceId = $statusId;
}

# 最後に処理したつぶやきの ID を保存
YAML::Tiny::DumpFile($FindBin::Bin . '/status.yml', {sinceId => $sinceId});
</pre>
<br>
ここまで作ってきたボットの内容を理解していれば、ほとんど自明のことしか無いかと思います。<br>
Yahoo 検索についても、Yahoo::Search という優れたライブラリがあるので、非常にシンプルに記述出来ています。<br>
<br>
<h4>あと書き</h4>
「Twitter ボットの作り方 Perl 編」の連載はこれで終わりになります。<br>
<br>
連載の終盤で、RSS や外部の API を利用するボットを作成しました。<br>
今のウェブ上ではこういった情報発信が広く行われており、複数のサービスを組み合わせることで多彩なボットを作成可能です。<br>
また、<a href="http://www.google.co.jp/search?hl=ja&q=%E3%82%B9%E3%82%AF%E3%83%AC%E3%82%A4%E3%83%94%E3%83%B3%E3%82%B0%E3%81%A8%E3%81%AF" target=_blank>スクレイピング</a>等の手法も組み合わせれば、まさにその組み合わせはアイディア次第。無限の可能性があると言っていいでしょう。<br>
もちろん、一発ネタのボットで注目を集めるというのも面白いそうです。<br>
<br>
この連載が、そんな素敵ボットを作ろうとする方の一助となれれば幸いです。<br>
<br>
<div class=navi>
<div class=prev><a href=http://www.otchy.net/20090719/making-of-twitter-bot-4/ >前へ</a></div>
<div class=index><a href=/20090726/making-of-twitter-bot-index/ >目次</a></div>
</div>
<div class=prev>
<a href=/20090719/making-of-twitter-bot-4/ >Twitter ボットの作り方 Perl 編 (4)</a>
</div>
<div class=next>
<a href=/20090726/making-of-twitter-bot-index/ >Twitter ボットの作り方 Perl 編 (目次)</a>
</div>
</main>
<footer>
<p class=categories>
<a href=/category/dev/ >Development</a>
</p>
<p class=categories>
<a href=/tag/twitter/ >twitter</a>
<a href=/tag/perl/ >perl</a>
</p>
</footer>
</body>
</html>
